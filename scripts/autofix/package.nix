# Heimcloud Ops auto-fix scripts (store derivations only; no secrets).
#
# Shared by modules/services/credentials/autofix-github.nix and
# scripts/autofix/test-local.sh, so the local test exercises exactly the
# build-time substitution that ships (a placeholder compared after
# replaceStrings once silently disabled the git credential helper).
#
#   nix-build scripts/autofix/package.nix --arg pkgs 'import <nixpkgs> {}' -A envWrapper
{
  pkgs,
  lib ? pkgs.lib,
  owner ? "hermes",
  group ? "hermes",
}: let
  # replaceStrings, plus a build-time guard that every placeholder was
  # substituted (a leftover "@name@" in shipped script text is a bug).
  substitute = name: from: to: file: let
    text = lib.replaceStrings from to (builtins.readFile file);
    leftover = builtins.filter (p: lib.hasInfix p text) from;
  in
    if leftover == []
    then text
    else throw "${name}: unsubstituted placeholder(s) ${toString leftover}";

  # Strip the repo shebang; pin python3 from pkgs so PATH is enough.
  extractBody = let
    raw = builtins.readFile ./extract-token.py;
    lines = lib.splitString "\n" raw;
  in
    if lines != [] && lib.hasPrefix "#!" (builtins.head lines)
    then lib.concatStringsSep "\n" (builtins.tail lines)
    else raw;

  extract = pkgs.writeScriptBin "heimcloud-autofix-extract-token" ''
    #!${pkgs.python3}/bin/python3
    ${extractBody}
  '';

  helper = pkgs.writeShellScriptBin "heimcloud-autofix-git-credential" (
    builtins.readFile ./git-credential-helper.sh
  );

  helperBin = "${helper}/bin/heimcloud-autofix-git-credential";

  # The wrapper injects the helper via GIT_CONFIG_COUNT/KEY/VALUE (see the
  # script). Only absolute store paths are substituted — never compared.
  envWrapper = pkgs.writeShellScriptBin "heimcloud-autofix-env" (
    substitute "heimcloud-autofix-env"
    ["@helper@" "@git@"]
    [helperBin "${pkgs.git}/bin/git"]
    ./heimcloud-autofix-env.sh
  );

  materialize = pkgs.writeShellScript "heimcloud-autofix-materialize" (
    substitute "heimcloud-autofix-materialize"
    ["@extract@" "@owner@" "@group@"]
    ["${extract}/bin/heimcloud-autofix-extract-token" owner group]
    ./materialize.sh
  );
in {
  inherit extract helper helperBin envWrapper materialize;
}
